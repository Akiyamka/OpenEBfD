extends SceneTree

var _assertions := 0
var _failures := 0
var _current_case := ""


func _run_case(case_name: String, test: Callable) -> void:
	_current_case = case_name
	var failures_before := _failures
	var assertions_before := _assertions
	test.call()
	# A runtime error aborts the case function where it stands, which would
	# otherwise read as a silent pass.
	if _assertions == assertions_before:
		_failures += 1
		printerr("FAIL: %s: the case ended before asserting anything" % case_name)
		return
	if _failures == failures_before:
		print("PASS: %s" % case_name)


func _run_async_case(case_name: String, test: Callable) -> void:
	_current_case = case_name
	var failures_before := _failures
	var assertions_before := _assertions
	await test.call()
	# A runtime error aborts the case function where it stands, which would
	# otherwise read as a silent pass.
	if _assertions == assertions_before:
		_failures += 1
		printerr("FAIL: %s: the case ended before asserting anything" % case_name)
		return
	if _failures == failures_before:
		print("PASS: %s" % case_name)


func _expect(condition: bool, message: String) -> void:
	_assertions += 1
	if condition:
		return
	_failures += 1
	printerr("FAIL: %s: %s" % [_current_case, message])


func _finish(label: String) -> void:
	if _failures > 0:
		printerr("%s: %d failures after %d assertions" % [label, _failures, _assertions])
		quit(1)
		return
	print("%s: %d assertions passed" % [label, _assertions])
	quit(0)
